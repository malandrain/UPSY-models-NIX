#! /bin/csh -f

# Compile all the UPSY code.
#
# Usage: ./compile_UPSY.csh  [VERSION]  [SELECTION]
#
#   [VERSION]: dev, perf
#
#     dev : developer's version; extra compiler flags (see Makefile_include, COMPILER_FLAGS_CHECK),
#           plus run-time assertions (i.e. DO_ASSERTIONS = true) and resource tracking (i.e.
#           DO_RESOURCE_TRACKING = true). Useful for tracking coding errors, but slow.
#
#     perf: performance version; all of the above disabled. Coding errors will much more often
#           simply result in segmentation faults, but runs much faster.
#
#   [SELECTION]: changed, clean
#
#     changed: (re)compile only changed modules. Works 99% of the time, fails when you change the
#              definition of a derived type without recompiling all of the modules thst use that
#              type. In that case, better to just do a clean compilation.
#
#     clean: recompile all modules. Always works, but slower.
#

# Safety
if ($#argv != 2) goto usage

echo ""

#  Confirm user's compilation choices
set version   = $argv[1]
if ($version == 'dev') then
  echo "dev: compiling UPSY, developers' version"
else if ($version == 'perf') then
  echo "perf: compiling UPSY, performance version"
else
  goto usage
endif

set selection = $argv[2]
if ($selection == 'changed') then
  echo "changed: (re)compiling changed modules only"
else if ($selection == 'clean') then
  echo "clean: recompiling all modules"
else
  goto usage
endif

echo ""

set compile_exit_code = 0
set in_build_dir = 0

# Define compiler flags based on version
if ($version == 'dev') then
  set compiler_flags = "-fdiagnostics-color=always -Og -Wall -ffree-line-length-none -cpp -Werror=implicit-interface -fimplicit-none -g -march=native -fcheck=all -fbacktrace -finit-real=nan -finit-integer=-42 -finit-character=33"
else if ($version == 'perf') then
  set compiler_flags = "-fdiagnostics-color=always -O3 -Wall -ffree-line-length-none -cpp -fimplicit-none -g -march=native"
endif

# Convert spaces to semicolons for CMake list format
set cmake_flags = `echo "$compiler_flags" | sed 's/ /;/g'`

# If no include directory exists, create it
if (! -d include) mkdir include

# If no build directory exists, create it
if (! -d build) mkdir build

# For a "clean" build, remove all build files first
if ($selection == 'clean') rm -rf build/*

# For a "changed" build, remove only the CMake cache file
if ($selection == 'changed') rm -f build/CMakeCache.txt

# Add git commit hash and package versions to the source code
csh -f ./src/UPSY/basic/git_commit_hash_and_package_versions/add_git_commit_hash_and_package_versions_to_code.csh "$compiler_flags"
if ($status != 0) then
  echo "Error: Failed to add git commit hash to the code"
  set compile_exit_code = 1
  goto cleanup
endif

# Use CMake to build UPSY, with Ninja to determine module dependencies;
# use different compiler flags for the development/performance build
cd build
set in_build_dir = 1

if ($version == 'dev') then

  cmake -G Ninja -DPETSC_DIR=`brew --prefix petsc` \
    -DDO_ASSERTIONS=ON \
    -DDO_RESOURCE_TRACKING=ON \
    -DHDF5_NO_FIND_PACKAGE_CONFIG_FILE=ON \
    -DEXTRA_Fortran_FLAGS="$cmake_flags" ..

else if ($version == 'perf') then

  cmake -G Ninja -DPETSC_DIR=`brew --prefix petsc` \
    -DDO_ASSERTIONS=OFF \
    -DDO_RESOURCE_TRACKING=OFF \
    -DHDF5_NO_FIND_PACKAGE_CONFIG_FILE=ON \
    -DEXTRA_Fortran_FLAGS="$cmake_flags" ..

endif

if ($status != 0) then
  echo "Error: CMake configuration failed"
  set compile_exit_code = 1
  goto cleanup
endif

ninja -v
if ($status != 0) then
  echo "Error: Ninja build failed"
  set compile_exit_code = 1
endif

cleanup:

if ($in_build_dir == 1) then
  cd ..
endif

# Delete git commit hash and package versions from the source code (restore to INVALID)
csh -f ./src/UPSY/basic/git_commit_hash_and_package_versions/delete_git_commit_hash_and_package_versions_from_code.csh
if ($status != 0) then
  echo "Error: Failed to delete git commit hash from the code"
  set compile_exit_code = 1
endif

exit $compile_exit_code

usage:

echo ""
echo "Usage: ./compile_UPSY.csh  [VERSION]  [SELECTION]"
echo ""
echo "  [VERSION]: dev, perf"
echo ""
echo "    dev : developer's version; extra compiler flags (see Makefile_include, COMPILER_FLAGS_CHECK),"
echo "          plus run-time assertions (i.e. DO_ASSERTIONS = true) and resource tracking (i.e."
echo "          DO_RESOURCE_TRACKING = true). Useful for tracking coding errors, but slow."
echo ""
echo "    perf: performance version; all of the above disabled. Coding errors will much more often"
echo "          simply result in segmentation faults, but runs much faster."
echo ""
echo "  [SELECTION]: changed, clean"
echo ""
echo "    changed: (re)compile only changed modules. Works 99% of the time, fails when you change the"
echo "             definition of a derived type without recompiling all of the modules thst use that"
echo "             type. In that case, better to just do a clean compilation."
echo ""
echo "    clean: recompile all modules. Always works, but slower."
echo ""

exit 1
